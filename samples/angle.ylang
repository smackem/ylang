SobelX := |-1  0  1
           -2  0  2
           -1  0  1|
SobelY := | 1  2  1
            0  0  0
           -1 -2 -1|

for pos in Bounds {
    @pos = grey(@pos.i)
}
flip()

for pos in Bounds {
    gx := convolute(pos, SobelX).r
    gy := convolute(pos, SobelY).r
    mag := hypot(gx, gy)
    color := nil
    angle := gx == 0 ? 0 : atan(gy / gx) * Rad2Deg + 90
    if angle >= 157.5 or angle < 22.5 {
        color = #ff0000 // 0 - red
    } else if angle < 67.5 {
        color = #00ff00 // 45 - green
    } else if angle < 112.5 {
        color = #0000ff // 90 - blue
    } else if angle < 157.5 {
        color = #ffff00 // 135 - yellow
    }
    @pos = color * mag/255
}
