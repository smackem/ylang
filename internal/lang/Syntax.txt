Program:
    | StatementList EOF

StatementList:
    | Statement StatementList
    | Statement

Statement:
    | IdentStatement
    | AT Atom EQ Expr
    | IfStatement
    | ForStatement
    | YIELD Expr
    | LOG LPAREN ParameterList RPAREN
    | BLT LPAREN Expr RPAREN
    | CommitStatement
    | RETURN Expr

IdentStatement:
    | IDENT COLONEQ Expr
    | IDENT EQ Expr
    | IDENT LBRACKET Expr RBRACKET EQ Expr
    | IDENT LPAREN ParameterListOpt RPAREN

IfStatement:
    | IF Expr LBRACE StatementList RBRACE
    | IF Expr LBRACE StatementList RBRACE ELSE LBRACE StatementList RBRACE
    | IF Expr LBRACE StatementList RBRACE ELSE IfStatement

ForStatement:
    | FOR IDENT IN Expr LBRACE StatementList RBRACE
    | FOR IDENT IN Molecule DOTDOT Molecule LBRACE StatementList RBRACE

CommitStatement:
    | COMMIT LPAREN Expr RPAREN
    | COMMIT

Expr:
    | Expr QMARK Expr COLON Expr
    | Expr OR AndExpr
    | AndExpr

AndExpr:
    | AndExpr AND Cond
    | Cond

Cond:
    | Term EQEQ Term
    | Term NEQ Term
    | Term GT Term
    | Term GE Term
    | Term LT Term
    | Term LE Term
    | Term

Term:
    | Term PLUS Molecule
    | Term MINUS Molecule
    | Term STAR Molecule
    | Term SLASH Molecule
    | Term PERCENT Molecule
    | Term IN Molecule
    | Molecule

Molecule:
    | MINUS Atom
    | NOT Atom
    | Molecule SEMICOLON Atom
    | Molecule DOT IDENT
    | Molecule LBRACKET Expr RBRACKET
    | Atom

MoleculeList:
    | Molecule MoleculeList
    | Molecule

Atom:
    | LPAREN Expr RPAREN
    | AT Atom
    | IDENT
    | NUMBER
    | STRING
    | COLOR
    | TRUE
    | FALSE
    | NIL
    | IDENT LPAREN ParameterListOpt RPAREN
    | LBRACKET MoleculeList RBRACKET
    | FunctionDef

ParameterListOpt:
    | ParameterList
    |

ParameterList:
    | Expr COMMA ParameterList
    | Expr

FunctionDef:
    | FN LPAREN IdentListOpt RPAREN LBRACKET StatementList RBRACKET
    | FN LPAREN IdentListOpt RPAREN ARROW Expr

IdentListOpt:
    | IdentList
    |

IdentList:
    | IDENT COMMA IdentList
    | IDENT