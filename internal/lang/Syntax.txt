Program:
    | StatementList EOF

StatementList:
    | Statement StatementList
    | Statement

Statement:
    | IdentStatement
    | AT Atom EQ Expr
    | IfStatement
    | ForStatement
    | YIELD Expr
    | LOG LPAREN ArgumentList RPAREN
    | BLT LPAREN Expr RPAREN
    | CommitStatement
    | RETURN Expr

IdentStatement:
    | IDENT COLONEQ Expr
    | IDENT EQ Expr
    | IDENT LBRACKET Expr RBRACKET EQ Expr
    | IDENT LPAREN ArgumentListOpt RPAREN

IfStatement:
    | IF Expr LBRACE StatementList RBRACE
    | IF Expr LBRACE StatementList RBRACE ELSE LBRACE StatementList RBRACE
    | IF Expr LBRACE StatementList RBRACE ELSE IfStatement

ForStatement:
    | FOR IDENT IN Expr LBRACE StatementList RBRACE
    | FOR IDENT IN Range LBRACE StatementList RBRACE

Range:
    | Expr DOTDOT Expr
    | Expr DOTDOT Expr DOTDOT Expr

CommitStatement:
    | COMMIT LPAREN Expr RPAREN
    | COMMIT

Expr:
    | Expr QMARK Expr COLON Expr
    | Expr OR AndExpr
    | AndExpr

AndExpr:
    | AndExpr AND Cond
    | Cond

Cond:
    | Concat EQEQ Concat
    | Concat NEQ Concat
    | Concat GT Concat
    | Concat GE Concat
    | Concat LT Concat
    | Concat LE Concat
    | Concat

Concat:
    | Tuple COLONCOLON Tuple

Tuple:
    | Term SEMICOLON Term

Term:
    | Term PLUS Molecule
    | Term MINUS Molecule
    | Term STAR Molecule
    | Term SLASH Molecule
    | Term PERCENT Molecule
    | Term IN Molecule
    | Molecule

Molecule:
    | MINUS Atom
    | NOT Atom
    | Molecule DOT IDENT
    | Molecule LBRACKET Expr RBRACKET
    | Molecule LBRACKET Expr DOTDOT Expr RBRACKET
    | Atom

MoleculeList:
    | Molecule MoleculeList
    | Molecule

Atom:
    | LPAREN Expr RPAREN
    | AT Atom
    | IDENT
    | NUMBER
    | STRING
    | COLOR
    | TRUE
    | FALSE
    | NIL
    | IDENT LPAREN ArgumentListOpt RPAREN
    | PIPE MoleculeList PIPE
    | LBRACE HashEntryListOpt RBRACE
    | LBRACKET ListElementListOpt RBRACKET
    | FunctionDef

ArgumentListOpt:
    | ArgumentList
    |

ArgumentList:
    | Expr COMMA ArgumentList
    | Expr

HashEntryListOpt:
    | HashEntryList
    | HashEntryList COMMA
    |

HashEntryList:
    | Expr COLON Expr COMMA HashEntryList
    | Expr COLON Expr

FunctionDef:
    | FN LPAREN IdentListOpt RPAREN LBRACKET StatementList RBRACKET
    | FN LPAREN IdentListOpt RPAREN ARROW Expr

IdentListOpt:
    | IdentList
    |

IdentList:
    | IDENT COMMA IdentList
    | IDENT

ListElementListOpt:
    | ListElementList
    | ListElementList COMMA
    |

ListElementList:
    | Expr COMMA ListElementList
    | Expr
